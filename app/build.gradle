plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}
apply from: "$project.rootDir/tools/consts.gradle"

def familyAppKeystorePropertiesFile = file("keys/keyStoreFamilyApp.properties")
def familyAppKeystoreProperties = new Properties()
familyAppKeystoreProperties.load(new FileInputStream(familyAppKeystorePropertiesFile))

android {
    namespace 'com.seva.starnet.casicalculator'
    compileSdk project.ext.compileSdkVersion

    defaultConfig {
        minSdk project.ext.minSdkVersion
        targetSdk project.ext.targetSdkVersion
        versionCode project.ext.versionCode
        versionName project.ext.versionName
        buildConfigField 'int', 'VERSION_CODE', "${project.ext.versionCode}"
        buildConfigField 'String', 'VERSION_NAME', "\"${project.ext.versionName}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file(familyAppKeystoreProperties['storeFile'])
            storePassword familyAppKeystoreProperties['storePassword']
            keyAlias familyAppKeystoreProperties['keyAlias']
            keyPassword familyAppKeystoreProperties['keyPassword']
        }
    }


    buildTypes {
        debug {
            debuggable = true
        }

        release {
            signingConfig signingConfigs.release
            debuggable = false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def FLAVOR_DIMENSION_RELEASE = "FLAVOR_DIMENSION_RELEASE"
    flavorDimensions FLAVOR_DIMENSION_RELEASE

    productFlavors {
        dev {
            dimension FLAVOR_DIMENSION_RELEASE
            applicationId "com.seva.starnet.casiCalculator.dev"
            matchingFallbacks = ['dev']
        }
        prod {
            dimension FLAVOR_DIMENSION_RELEASE
            applicationId "com.seva.starnet.casiCalculator"
            matchingFallbacks = ['prod']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.4.0'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    implementation platform('androidx.compose:compose-bom:2022.10.00')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.3.7'
    implementation 'com.google.firebase:firebase-analytics-ktx:21.3.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation platform('androidx.compose:compose-bom:2022.10.00')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'

    implementation "io.insert-koin:koin-core:$koinVersion"
    implementation "io.insert-koin:koin-android:$koinVersion"
    implementation "io.insert-koin:koin-androidx-navigation:$koinVersion"


    implementation("androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version")
    implementation("androidx.navigation:navigation-compose:2.5.3")
}